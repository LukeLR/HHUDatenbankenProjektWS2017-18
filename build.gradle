buildscript {
    repositories {
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.1'
    }
}

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'maven-publish'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'com.github.johnrengelman.shadow'

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {
    compile 'com.github.Phlegethon90:ApplicationKit:v1.1.0'
    testCompile 'org.junit.jupiter:junit-jupiter-api:5.0.2'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

task deleteDatabase(type: Delete) {
	group 'Database'
	description 'Deletes the SQLite Database file'
	
	delete 'database.sqlite'
	delete 'database.sqlite-shm'
	delete 'database.sqlite-wal'
}

task initDatabase {
	group 'Database'
	description 'Initializes the SQLite Database file'
	
	doLast {
		def proc = './init-database.sh'.execute();
		proc.waitFor();
		println "return code: ${ proc.exitValue()}"
		println "stderr: ${proc.err.text}"
		println "stdout: ${proc.in.text}"
	}
}

task recreateDatabase (dependsOn: ['deleteDatabase', 'initDatabase']) {
	group 'Database'
	description 'Recreates the SQLite Database file: Deletes it and inits it afterwards.'
}

group 'de.hhu.cs.dbs.internship.project'
version '2018.1.0'
description = 'Project for all semesters'
sourceCompatibility = 1.8
mainClassName = 'de.hhu.cs.dbs.internship.project.Project'